%% Copyright (C) 2009-2010, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{Enumeration}

Prototype of enumeration types.

\subsection{Prototypes}
\begin{refObjects}
\item[RangeIterable]
\item[Container]
\end{refObjects}

\subsection{Examples}

See \autoref{sec:lang:enum}.

\subsection{Construction}

An \lstinline{Enumeration} is created with two arguments: the name of the
enumeration type, and the list of possible values.  Most of the time, it is
a good idea to store it in a variable with the same name.

\begin{urbiscript}[firstnumber=1]
var Direction = Enumeration.new("Direction", ["up", "down", "left", "right"]);
[00000001] Direction
Direction.up;
[00000002] up
\end{urbiscript}

The following syntax is equivalent.

\begin{urbiunchecked}
enum Direction
{
  up,
  down,
  left,
  right
};
[00000001] Direction
\end{urbiunchecked}

The created values are derive from the created enumeration type.

\begin{urbiassert}
Direction.isA(Enumeration);
Direction.up.isA(Direction);
\end{urbiassert}

\subsection{Slots}
\begin{urbiscriptapi}

\item[asList]
  Synonym for \refSlot{values}.
\begin{urbiassert}
Direction.asList
  == [Direction.up, Direction.down, Direction.left, Direction.right];
\end{urbiassert}

  Since it also derives from \refObject{RangeIterable}, this enables all
  its features.  For instance:

\begin{urbiscript}
Direction.each(function (var d) { echo(d) });
[00000001] *** up
[00000001] *** down
[00000001] *** left
[00000001] *** right

for (var d in Direction)
  echo(d);
[00000001] *** up
[00000001] *** down
[00000001] *** left
[00000001] *** right

for| (var d in Direction)
  echo(d);
[00000001] *** up
[00000001] *** down
[00000001] *** left
[00000001] *** right

assert
{
  Direction.any(closure (var v) { v == Direction.up });
};
\end{urbiscript}

\item[asString]
  The name of the enumeration, or the name of the member if applied to a
  member.
\begin{urbiassert}
Direction.asString    == "Direction";
Direction.up.asString == "up";
\end{urbiassert}

\item[has](<v>)%
  Whether \var{v} is a member of \this.  Since \refObject{Enumeration}
  derives from \refObject{Container}, it provides all its features.
\begin{urbiscript}
enum CardinalDirection { north, east, south, west }|;

assert
{
  Direction.has(Direction.up);
  Direction.up in Direction;
  12 not in Direction;
  CardinalDirection.south not in Direction;
};
\end{urbiscript}


\item[name]
  The name of the enumeration.  See also \refSlot{asString}.
\begin{urbiassert}
Direction.name    == "Direction";
Direction.up.name == "Direction";
\end{urbiassert}


\item[size]
  Number of possible values in the enumeration type. Equivalent to
  \lstinline{values.size}.
\begin{urbiassert}
Direction.size == 4;
\end{urbiassert}


\item[values]
  The list of values.
\begin{urbiassert}
Direction.values
  == [Direction.up, Direction.down, Direction.left, Direction.right];
\end{urbiassert}
\end{urbiscriptapi}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
