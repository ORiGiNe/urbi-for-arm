%% Copyright (C) 2009-2010, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\section{File}

\subsection{Prototypes}
\begin{refObjects}
\item[Object]
\end{refObjects}

\subsection{Construction}

Files may be created from a \refObject{String}, or from a \refObject{Path}.
Using \lstinline|new|, the file must exist on the file system, and must be a
file.  You may use \refSlot{create} to create a file that does not exist (or
to override an existing one).

\begin{urbiscript}[firstnumber=1]
File.create("file.txt");
[00000001] File("file.txt")

File.new(Path.new("file.txt"));
[00000001] File("file.txt")
\end{urbiscript}

You may use \refObject{InputStream} and \refObject{OutputStream} to
read or write to Files.

\subsection{Slots}

\begin{urbiscriptapi}
\item[asList]
  Read the file, and return its content as a list of its lines.
\begin{urbiscript}
File.save("file.txt", "1\n2\n");
assert(File.new("file.txt").asList == ["1", "2"]);
\end{urbiscript}

\item[asPath] A \refObject{Path} being the path of the file.

\item[asPrintable]
\begin{urbiscript}
File.save("file.txt", "1\n2\n");
assert(File.new("file.txt").asPrintable == "File(\"file.txt\")");
\end{urbiscript}

\item[asString]
  The name of the opened file.
\begin{urbiscript}
File.save("file.txt", "1\n2\n");
assert(File.new("file.txt").asString == "file.txt");
\end{urbiscript}

\item[basename]
  Return a \refObject{String} containing the path of the file without
  its dirname.

\item[content]
  The content of the file as a \refObject{Binary} object.
\begin{urbiscript}
File.save("file.txt", "1\n2\n");
assert
{
  File.new("file.txt").content == Binary.new("", "1\n2\n");
};
\end{urbiscript}

\item[copy](<filename>)
  Copy the file to a new file named \var{filename}.
\begin{urbiscript}
File.save("file", "content");
var file = File.new("file");
[00000001] File("file")
var file2 = file.copy("file2");
[00000002] File("file2")
assert(file2.content == file.content);
file.remove;
file2.remove;
\end{urbiscript}

\item[copyInto](<dirname>)
  \experimental{}

  Copy file into \var{dirname} directory.
\begin{urbiscript}
var dir = Directory.create("dir")|;
file = File.create("file")|;
file.copyInto(dir);
[00000001] File("dir/file")
file;
[00000002] File("file")
dir.content;
[00000003] ["file"]
dir.removeAll;
file.remove;
\end{urbiscript}

\item[create](<name>)%
  If the file \var{name} does not exist, create it and a return a File to
  it.  Otherwise, first empty it.  See \refObject{OutputStream} for methods
  to add content to the file.
\begin{urbiscript}
var p = Path.new("create.txt") |
assert (!p.exists);

// Create the file, and put something in it.
var f = File.create(p)|;
var o = OutputStream.new(f)|;
o << "Hello, World!"|;
o.close;

assert
{
  // The file exists, with the expect contents.
  p.exists;
  f.content.data == "Hello, World!";

  // If we create is again, it is empty.
  File.create(p).isA(File);
  f.content.data == "";
};
\end{urbiscript}

\item[lastModifiedDate]
  \experimental{}

  Return a \refObject{Date} object stating when the file was last modified.

\item[moveInto](<dirname>)
  \experimental{}

  Move file into \var{dirname} directory.
\begin{urbiscript}
dir = Directory.create("dir")|;
file = File.create("file")|;
file.moveInto(dir);
[00000001] File("dir/file")
file;
[00000001] File("dir/file")
dir.content;
[00000001] ["file"]
dir.removeAll;
\end{urbiscript}

\item[remove]
  Remove the current file.  Returns void.
\begin{urbiscript}[firstnumber=1]
var p = Path.new("foo.txt") |
p.exists;
[00000002] false

var f = File.create(p);
[00000003] File("foo.txt")
p.exists;
[00000004] true

f.remove;
p.exists;
[00000006] false
\end{urbiscript}

\item[rename](<name>)%
  Rename the file to \var{name}.  If the target exists, it is replaced by
  the opened file. Return the file renamed.
\begin{urbiscript}
File.save("file.txt", "1\n2\n");
File.new("file.txt").rename("bar.txt");
[00000001] File("bar.txt")
assert
{
  !Path.new("file.txt").exists;
  File.new("bar.txt").content.data == "1\n2\n";
};
\end{urbiscript}

\item[save](<name>, <content>)
  Use \refSlot{create} to create the File named \var{name}, store the
  \var{content} in it, and close the file.  Return void.
\begin{urbiassert}
File.save("file.txt", "1\n2\n").isVoid;
File.new("file.txt").content.data == "1\n2\n";
\end{urbiassert}

\item[size]
  The size of the file.
\begin{urbiscript}
File.save("file.txt", "1234");
File.new("file.txt").size;
[00000001] 4
\end{urbiscript}
\end{urbiscriptapi}


%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
