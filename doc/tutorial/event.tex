%% Copyright (C) 2009-2012, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\chapter{Event-based Programming}
\label{sec:tut:event-prog}

When dealing with highly interactive agent programming, sequential
programming is inconvenient. We want to react to external, random
events, not execute code linearly with a predefined flow. \us has a
strong support for event-based programming.

\section{Watchdog constructs}

The first construct we will study uses the \lstinline|at| keyword. Given a
condition and a statement, \lstinline|at| will evaluate the statement each
time the condition \emph{becomes} true. That is, when a rising edge occurs
on the condition.

\begin{urbiscript}[firstnumber=1]
var x = 0;
[00000000] 0
at (x > 5)
  echo("ping");
x = 5;
[00000000] 5
// This triggers the event.
x = 6;
[00000000] 6
[00000000] *** ping
// Does not trigger, since the condition is already true.
x = 7;
[00000000] 7
// The condition becomes false here.
x = 3;
[00000000] 3

x = 10;
[00000000] 10
[00000000] *** ping
\end{urbiscript}

An \lstinline|onleave| block can be appended to execute an expression when
the expression \emph{becomes} false --- that is, on falling edges.

\begin{urbiscript}[firstnumber=1]
var x = false;
[00000000] false
at (x)
  echo("x")
onleave
  echo("!x");
x = true;
[00000000] true
[00000000] *** x
x = false;
[00000000] false
[00000000] *** !x
\end{urbiscript}

See \autoref{sec:lang:at} for more details on \lstinline|at| statements.

The \lstinline|whenever| construct is similar to \lstinline|at|, except the
expression evaluation is systematically restarted when it finishes as long
as the condition stands true.

\begin{urbiscript}[firstnumber=1]
var x = false;
[00000000] false
whenever (x)
{
  echo("ping");
  sleep(1s);
};
x = true;
[00000000] true
sleep(3s);
// Whenever keeps triggering
[00000000] *** ping
[00001000] *** ping
[00002000] *** ping
x = false;
[00002000] false
// Whenever stops triggering
\end{urbiscript}

Just like \lstinline|at| has \lstinline|onleave|, \lstinline|whenever|
has \lstinline|else|: the given expression is evaluated as long as the
condition is false.

\begin{urbiscript}[firstnumber=1]
var x = false;
[00002000] false
whenever (x)
{
  echo("ping");
  sleep(1s);
}
else
{
  echo("pong");
  sleep(1s);
};
sleep (3s);
[00000000] *** pong
[00001000] *** pong
[00002000] *** pong
x = true;
[00003000] true
sleep (3s);
[00003000] *** ping
[00004000] *** ping
[00005000] *** ping
x = false;
[00006000] false
sleep (2s);
[00006000] *** pong
[00007000] *** pong
\end{urbiscript}

\section{Events}
\label{sec:tut:events}

In addition to monitoring an expression with a watchdog, \us enables you to
define events that can be caught with the \lstinline|at| and
\lstinline|whenever| constructs we saw earlier. You can create events by
instantiating the \refObject{Event} prototype. They can then be emitted with
the \lstinline|!| keyword.

\subsection{Emitting Events}

\begin{urbiscript}[firstnumber=1]
var myEvent = Event.new;
[00000000] Event_0xb5579008
at (myEvent?)
  echo("ping");
myEvent!;
[00000000] *** ping
// events work well with parallelism
myEvent! & myEvent!;
[00000000] *** ping
[00000000] *** ping
\end{urbiscript}

Both \lstinline|at| and \lstinline|whenever| have the same behavior on
punctual events. However, if you emit an event for a given duration,
\lstinline|whenever| will keep triggering for this duration, contrary to
\lstinline|at|.

% FIXME: sleep(1s) is required at the end to make this test pass ...
\begin{urbiunchecked}[firstnumber=1]
var myEvent = Event.new;
[00000000] Event_0xb558a588
whenever (myEvent?)
{
  echo("ping (whenever)")|
  sleep(200ms)
};
at (myEvent?)
{
  echo("ping (at)")|
  sleep(200ms)
};
// Emit myEvent for .3 second.
myEvent! ~ 300ms;
[00000000] *** ping (whenever)
[00000100] *** ping (whenever)
[00000000] *** ping (at)
\end{urbiunchecked}

\subsection{Emitting events with a payload}
\label{sec:tut:events:payload}

Events behave very much like ``channels'': listeners use \lstinline|at| or
\lstinline|whenever|, and producers use \lstinline|!|.  Fortunately, the
messages can include a \dfn{payload}, i.e., something sent in the
``message''.  The Event then behaves very much like an identifier of the
message type.  To send/catch the payload, just pass arguments to
\lstinline|!| and \lstinline|?|:

\begin{urbiscript}
var event = Event.new;
[00000000] Event_0x0

at (event?(var payload))
  echo("received: " + payload)
onleave
  echo("had received: " + payload);

event!(1);
[00000008] *** received: 1
[00000009] *** had received: 1

event!(["string", 124]);
[00000010] *** received: ["string", 124]
[00000011] *** had received: ["string", 124]
\end{urbiscript}

Like functions, events have an \dfn{arity}, i.e., they depend on the number
of arguments: \lstinline|at (\var{event}?(\var{arg}))| will only match
emissions whose payload contain exactly one argument, i.e.,
\lstinline|\var{event}!(\var{arg})|.

\begin{urbiscript}
// Too many arguments.
event!(1, 2);

// Not enough arguments.
event!;
event!();
\end{urbiscript}

Event handlers that do not specify their arity (i.e., without parentheses)
match event emissions of any arity.

\begin{urbiscript}
at (event?)
  echo("received an event")
onleave
  echo("had received an event");

event!;
[00000014] *** received an event
[00000015] *** had received an event

event!(1);
[00000016] *** received: 1
[00000017] *** had received: 1
[00000018] *** received an event
[00000019] *** had received an event

event!(1, 2);
[00000020] *** received an event
[00000021] *** had received an event
\end{urbiscript}

Actually, the feature is much more powerful than this: full pattern matching
applies, as with the \lstinline|switch|/\lstinline|case| construct.

\begin{urbiscript}
var e = Event.new|;

at (e?)
  echo("e");

at (e?(var x))
  echo("e(x)");

at (e?(1))
  echo("e(1)");

at (e?(var x) if x.isA(Float) && x % 2)
  echo("e(odd)");

// Payload must be a list of three members, the first two being 1 and 2, and
// the third one being greater than 2, when converted as a Float.
at (e?([1, 2, var x]) if 2 < x.asFloat)
  echo("e([1, 2, x = %s])" % x);

e!;
[00000845] *** e

e!(0);
[00011902] *** e
[00011902] *** e(x)

e!(1);
[00023327] *** e
[00023327] *** e(x)
[00023327] *** e(1)
[00023327] *** e(odd)

e!([1, 2, 1]);
[00024327] *** e
[00024327] *** e(x)

e!([1, 2, 3]);
[00025327] *** e
[00025327] *** e(x)
[00025327] *** e([1, 2, x = 3])

e!([1, 2, "4"]);
[00026327] *** e
[00026327] *** e(x)
[00026327] *** e([1, 2, x = 4])
\end{urbiscript}

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
