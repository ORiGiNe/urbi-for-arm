%% Copyright (C) 2009-2012, Gostai S.A.S.
%%
%% This software is provided "as is" without warranty of any kind,
%% either expressed or implied, including but not limited to the
%% implied warranties of fitness for a particular purpose.
%%
%% See the LICENSE file for more information.

\chapter{First Steps}
\label{sec:tut:first}

This section expects that you already know how to run \command{urbi}.  If
not, please first see \autoref{sec:tut:started}.

This section introduces the most basic notions to write \us code. Some
aspects are presented only minimally.  The goal of this section is to
bootstrap yourself with the \us language, to be able to study more in-depth
examples afterward.

\section{Comments}

Commenting your code is crucial, so let's start by learning how to do this
in \us. \dfn{Comments} are ignored by the interpreter, and can be left as
documentation, reminder, \ldots \us supports \langC and \Cxx style comments:

\begin{itemize}
\item \langC style comments start with \textcmt{/*} and end with \textcmt{*/}.
  Contrary to \langC/\Cxx, this type of comments does nest.
\item \Cxx style comments start with \textcmt{//} and last until the end of
  the line.
\end{itemize}

\begin{urbiscript}[firstnumber=1]
1; // This is a C++ style comment.
[00000000] 1

2 + /* This is a C-style comment. */ 2;
[00000000] 4

"foo" /* You /* can /* nest */ */ comments. */ "bar";
[00000000] "foobar"
\end{urbiscript}

\autoref{sec:tut:started} introduced some of the conventions used in this
document: frames such as the previous one denote ``\us sessions'', i.e.,
dialogs between \urbi and you.  The output is prefixed by a number between
square brackets: this is the date (in milliseconds since the server was
launched) at which that line was sent by the server. This is useful at
occasions, since \urbi is meant to run many parallel commands.  Since these
timestamps are irrelevant in documentation, they will often be filled with
zeroes.  More details about the typesetting of this document (and the other
kinds of frames) can be found in \autoref{sec:notations}.

\section{Literal values}

Several special kinds of ``values'' can be entered directly with a specific
syntax.  They are called \dfn{literals}, or sometimes \dfn{manifest values}.
We just met a first kind of literals: integers.  There are several others,
such as:

\begin{itemize}
\item \refObject[Float]{floats}: floating point numbers.
\begin{urbiscript}
42; // Integer literal.
[00000000] 42

3.14; // Floating point number literal.
[00000000] 3.14
\end{urbiscript}

\item \refObject[String]{strings}: character strings.
\begin{urbiscript}
"string";
[00000000] "string"
\end{urbiscript}

\item \refObject[List]{lists}: ordered collection of values.
\begin{urbiscript}
[1, 2, "a", "b"];
[00000000] [1, 2, "a", "b"]
\end{urbiscript}

\item \refObject[Dictionary]{dictionaries}: unordered collection of
  associations.
\begin{urbiscript}
["a" => 1, "b" => 2, "3" => "three"];
[00000000] ["3" => "three", "a" => 1, "b" => 2]
\end{urbiscript}

\item \refObject{nil}: neutral value, or value placeholder. Think of it as
  the value that fits anywhere.
\begin{urbiscript}
nil;
\end{urbiscript}

\item \refObject{void}: absence of value. Think of it as the value that fits
  nowhere.
\begin{urbiscript}
void;
\end{urbiscript}
\end{itemize}

These examples highlight some points:
\begin{itemize}
\item \refObject[List]{Lists} and \refObject[Dictionary]{dictionaries} in
  \us are heterogeneous. That is, they can hold values of different types.
\item The printing of \refObject{nil} and \refObject{void} is empty.
\item There are many hyperlinks in this document: clicking on names such as
  \refObject{Dictionary} will drive you immediately to its specifications.
  This is also true for slots, such as \refSlot[String]{size}.  If you're
  looking for something, \hyperref[sec:index]{check the index!}
\end{itemize}

\section{Function calls}

You can call functions with the classical, mathematical notation.

\begin{urbiscript}
cos(0); // Compute cosine
[00000000] 1
max(1, 3); // Get the maximum of the arguments.
[00000000] 3
max(1, 3, 4, 2);
[00000000] 4
\end{urbiscript}

Again, the result of the evaluation are printed out. You can see here
that function in \us can be variadic, that is, take different number
of arguments, such as the \lstinline{max} function. Let's now try the
\lstindex{echo} function, that prints out its argument.

\begin{urbiscript}
echo("Hello world!");
[00000000] *** Hello world!
\end{urbiscript}

The server prints out \lstinline{Hello world!}, as expected. Note that this
output is still prepended with the time stamp. Since \lstinline{echo}
returns \lstinline{void}, no evaluation result is printed.

\section{Variables}\index{variable}
Variables can be introduced with the \lstinline{var} keyword, given a
name and an initial value. They can be assigned new values with the
\lstinline{=} operator.

\begin{urbiscript}
var x = 42;
[00000000] 42
echo(x);
[00000000] *** 42
x = 51;
[00000000] 51
x;
[00000000] 51
\end{urbiscript}

Note that, just as in \Cxx, assignments return the (right-hand side) value, so
you can write code like ``\lstinline|x = y = 0|''. The rule for valid
identifiers is also the same as in \Cxx: they may contain alphanumeric
characters and underscores, but they may not start with a digit.

You may omit the initialization value, in which case it defaults to
\lstinline|void|.

\begin{urbiscript}
var y;
y;
// Remember, the interpreter remains silent because void is printed out
// as nothing.  You can convince yourself that y is actually void with
// the following methods.
y.asString;
[00000000] "void"
y.isVoid;
[00000000] true
\end{urbiscript}

\section{Scopes}
\dfn{Scopes} are introduced with curly brackets (\lstinline|{}|).  They can
contain any number of statements. Variables declared in a scope only exist
within this scope.

\begin{urbicomment}
removeSlots("x");
\end{urbicomment}
\begin{urbiscript}
{
  var x = "test";
  echo(x);
};
[00000000] *** test
// x is no longer defined here
x;
[00000073:error] !!! lookup failed: x
\end{urbiscript}

Note that the interpreter waits for the whole scope to be input to evaluate
it. Also note the mandatory terminating semicolon after the closing curly
bracket.

\section{Method calls}

Methods are called on objects with the dot (\lstinline{.}) notation as in
\Cxx.  Method calls can be chained.  Methods with no arguments don't require
the parentheses.

\begin{urbiscript}
0.cos();
[00000000] 1
"a-b-c".split("-");
[00000000] ["a", "b", "c"]
// Empty parentheses are optional
"foo".length();
[00000000] 3
"foo".length;
[00000000] 3
// Method call can be chained
"".length.cos;
[00000000] 1
\end{urbiscript}

In \lstinline|obj.method|, we say that \lstinline{obj} is the
\dfn{target}, and that we are sending him the \lstinline{method}
\dfn{message}.

\section{Function definition}

You know how to call routines, let's learn how to write
some. Functions can be declared thanks to the \lstinline{function}
keyword, followed by the comma separated, parentheses surrounded list
of formal arguments, and the body between curly brackets.

\begin{urbiscript}
// Define myFunction
function myFunction()
{
  echo("Hello world");
  echo("from my function!");
};
[00000000] function () {
[:]  echo("Hello world");
[:]  echo("from my function!");
[:]}

// Invoke it
myFunction();
[00000000] *** Hello world
[00000000] *** from my function!
\end{urbiscript}

Note the strange output after you defined the function. \us seems to
be printing the function you just typed in again. This is because
a function definition evaluates to the freshly created function.

Functions are first class citizen: they are values, just as
\lstinline{0} or \lstinline{"foobar"}.  The evaluation of a function
definition yields the new function, and as always, the interpreter
prints out the evaluation result, thus showing you the function
again:

\begin{urbiscript}
// Work in a scope.
{
  // Define f
  function f()
  {
    echo("f")
  };
  // This does not invoke f, it returns its value.
  f;
};
[00000000] function () { echo("f") }
{
  // Define f
  function f()
  {
    echo("Hello World");
  };
  // This actually calls f
  f();
};
[00000000] *** Hello World
\end{urbiscript}

Here you can see that \lstinline{f} is actually a simple value. You can just
evaluate it to see its value, that is, its body. By adding the
parentheses, you can actually call the function. This is a difference
with methods calling, where empty parentheses are optional: method are
always evaluated, you cannot retrieve their functional value --- of
course, you can with a different construct, but that's not the point
here.

Since this output is often irrelevant, most of the time it is hidden in this
documentation using the \lstinline'|;' trick.  When a statement is
``missing'', an empty statement (\lstinline|{}|) is inserted.  So
\lstinline'\var{code}|;' is actually equivalent to
\lstinline'\var{code} | {};', which means ``run \var{code}, then run
\lstinline'{}' and return its value''.  Since the value of \lstinline'{}' is
\lstinline'void', which is not displayed, this is a means to discard the
result of a computation, and avoid that something is printed.  Contrast the
two following function definitions.

\begin{urbiscript}
function sum(a, b, c)
{
  return a + b + c;
};
[00003553] function (var a, var b, var c) { return a.'+'(b).'+'(c) }
function sum2(a, b, c)
{
  return a + b + c;
}|;
sum(20, 2, 20);
[00003556] 42
\end{urbiscript}

The \lstinline{return} keyword breaks the control flow of a function
(similarly to the way \lstinline{break} interrupts a loop) and returns the
control flow to the caller.  It accepts an optional argument, the value to
return to the caller.

In \us, if no \lstinline{return} statement is executed, the value of the
last expression is returned.  Actually, refrained from using
\lstinline{return} when you don't need it, it is both less readable (once
you get used to this programming style), and less efficient
(\autoref{sec:guideline:return}).

\begin{urbiscript}
function succ(i) { i + 1 }|;
succ(50);
[00000000] 51
\end{urbiscript}

\section{Conclusion}

You're now up and running with basic \us code, and we can dive in
details into advanced \us code.

%%% Local Variables:
%%% coding: utf-8
%%% mode: latex
%%% TeX-master: "../urbi-sdk"
%%% ispell-dictionary: "american"
%%% ispell-personal-dictionary: "../urbi.dict"
%%% fill-column: 76
%%% End:
