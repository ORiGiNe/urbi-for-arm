/*
 * Copyright (C) 2010, 2011, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

class System.PackageInfo
{
  var components = [ => ];

  function init (var name)
  {
    var this.name = name |
    var this.dependencies = [] |
    System.PackageInfo.components[name] = this
  };

  // A function to make initialization dependencies easier to manage.
  function banner()
  {
    ["*" * 56]
    + copyright(false) +
    ["",
     "This program comes with ABSOLUTELY NO WARRANTY.  It can",
     "be used under certain conditions.  Type `license;',",
     "`authors;', or `copyright;' for more information.",
     "",
     "Check our community site: http://www.urbiforge.org.",
     "*" * 56];
  };

  function copyright(var deep = true)
  {
    [
      "%s %s" % [name, versionRev],
      "Copyright (C) %s %s%s"
        % [copyrightYears, copyrightHolder,
           { if (copyrightHolder[copyrightHolder.size-1] == ".") "" else "." } ]
    ]
    +
    {
      if (deep && dependencies)
        dependencies
          .map(methodToFunction("copyright"))
          .foldl([].methodToFunction("+"), [""])
      else
        [];
    }
  };

  function depend(var other)
  {
    dependencies += [other]|;
  };
};

load("urbi/package-info/urbi.u");
load("urbi/package-info/libport.u");

do (System.PackageInfo)
{
  var copyrightHolder = "@PACKAGE_COPYRIGHT_HOLDER@";
  var copyrightYears = "@PACKAGE_COPYRIGHT_YEARS@";
  components["Urbi"].depend(components["Libport"]);
  // Backward compat.
  components["Urbi SDK"] = components["Urbi"];
};
