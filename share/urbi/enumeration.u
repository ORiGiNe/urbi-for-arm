/*
 * Copyright (C) 2010, Gostai S.A.S.
 *
 * This software is provided "as is" without warranty of any kind,
 * either expressed or implied, including but not limited to the
 * implied warranties of fitness for a particular purpose.
 *
 * See the LICENSE file for more information.
 */

requireFile("urbi/list.u");
requireFile("urbi/channel.u");
requireFile("urbi/container.u");
requireFile("urbi/global.u");
requireFile("urbi/range-iterable.u");
requireFile("urbi/control.u");

class Global.Enumeration : Global.RangeIterable
{
  var this.name = "Enumeration";
  var this.values = [];
  protos << Container;

  function init(name, values)
  {
    this.name = name|
    this.values = []|
    this.type = name|
    var i = 0|
    for| (var value in values)
    {
      var val = clone |
      var val.asString = value |
      setSlot(value, val)|
      this.values << val|
    }
  };

  function asList()
  {
    values
  };

  function asString()
  {
    name
  };

  function has(var f)
  {
    asList.has(f)
  };

  function size()
  {
    asList.size
  };
};
